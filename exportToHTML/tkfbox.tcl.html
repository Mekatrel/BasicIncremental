<html>
<head>
<title>tkfbox.tcl</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<style type="text/css">
.s0 { color: #a9b7c6;}
</style>
</head>
<body bgcolor="#2b2b2b">
<table CELLSPACING=0 CELLPADDING=5 COLS=1 WIDTH="100%" BGCOLOR="#606060" >
<tr><td><center>
<font face="Arial, Helvetica" color="#000000">
tkfbox.tcl</font>
</center></td></tr></table>
<pre><span class="s0"># tkfbox.tcl --</span>
<span class="s0">#</span>
<span class="s0">#	Implements the &quot;TK&quot; standard file selection dialog box.  This dialog</span>
<span class="s0">#	box is used on the Unix platforms whenever the tk_strictMotif flag is</span>
<span class="s0">#	not set.</span>
<span class="s0">#</span>
<span class="s0">#	The &quot;TK&quot; standard file selection dialog box is similar to the file</span>
<span class="s0">#	selection dialog box on Win95(TM).  The user can navigate the</span>
<span class="s0">#	directories by clicking on the folder icons or by selecting the</span>
<span class="s0">#	&quot;Directory&quot; option menu.  The user can select files by clicking on the</span>
<span class="s0">#	file icons or by entering a filename in the &quot;Filename:&quot; entry.</span>
<span class="s0">#</span>
<span class="s0"># Copyright (c) 1994-1998 Sun Microsystems, Inc.</span>
<span class="s0">#</span>
<span class="s0"># See the file &quot;license.terms&quot; for information on usage and redistribution</span>
<span class="s0"># of this file, and for a DISCLAIMER OF ALL WARRANTIES.</span>
<span class="s0">#</span>

<span class="s0">namespace eval ::tk::dialog {}</span>
<span class="s0">namespace eval ::tk::dialog::file {</span>
    <span class="s0">namespace import -force ::tk::msgcat::*</span>
    <span class="s0">variable showHiddenBtn 0</span>
    <span class="s0">variable showHiddenVar 1</span>

    <span class="s0"># Create the images if they did not already exist.</span>
    <span class="s0">if {![info exists ::tk::Priv(updirImage)]} {</span>
	<span class="s0">set ::tk::Priv(updirImage) [image create photo -data {</span>
	    <span class="s0">iVBORw0KGgoAAAANSUhEUgAAABYAAAAWCAYAAADEtGw7AAAABmJLR0QA/gD+AP7rGN</span>
	    <span class="s0">SCAAAACXBIWXMAAA3WAAAN1gGQb3mcAAAACXZwQWcAAAAWAAAAFgDcxelYAAAENUlE</span>
	    <span class="s0">QVQ4y7WUbWiVZRjHf/f9POcc9+Kc5bC2aIq5sGG0XnTzNU13zAIlFMNc9CEhTCKwCC</span>
	    <span class="s0">JIgt7AglaR0RcrolAKg14+GBbiGL6xZiYyy63cmzvu7MVznnOe537rw7bDyvlBoT/c</span>
	    <span class="s0">n+6L3/3nf13XLZLJJP+HfICysjKvqqpq+rWKysvLR1tbW+11g+fPn/+bEGIe4KYqCs</span>
	    <span class="s0">Owu66u7oG2trah6wJrrRc0NTVhjME5h7Vj5pxzCCE4duxYZUdHx/aGhoZmgJ+yb+wF</span>
	    <span class="s0">uCO19RmAffv25f8LFslkktraWtvU1CS6u7vRWmOtxVpbAPu+T0tLS04pFU/J34Wd3S</span>
	    <span class="s0">cdFtlfZWeZBU4IcaS5uXn1ZLAEMMY4ay1aa4wx/zpKKYIgoL6+vmjxqoXe5ZLTcsPq</span>
	    <span class="s0">bTyycjODpe1y3WMrvDAMV14jCuW0VhhjiJQpOJ5w7Zwjk8/y9R+vsHHNNq6oFMrkeX</span>
	    <span class="s0">BxI+8d2sktap3YvOPD0lRQrH+Z81fE7t3WB4gihVKazsuaA20aKSUgAG/seQdy2l6W</span>
	    <span class="s0">37+EyopqTv39I6HJUT2zlnlza2jLdgiTaxwmDov6alLHcZUTzXPGGAauWJbfO4dHl9</span>
	    <span class="s0">bgJs3HyfNf0N4ZsOa+jbT3/ownY/hO09p1kBULtjBw+Tvq7xzwauds4dWPDleAcP5E</span>
	    <span class="s0">xlprgtBRUZRgYCRPTzoHwEi2g6OnX+eFrW/RM9qBE4p43CeTz5ATaU6nDrFm2cPs/+</span>
	    <span class="s0">E1SopqkZ7MFJqntXZaa7IKppckwIEvJbg8LWd28OT6nVihCPQQ8UScWCLGqO4hXuQx</span>
	    <span class="s0">qDtJ204eWrqWb1ufRspwtABWaqx5gRKUFSdwDnxPcuLcyyxbuIyaqntIBV34MY9YzC</span>
	    <span class="s0">Owg+S9YeJFkniRpGPkCLMrZzG3+jbktA/KClMxFoUhiKC0OAbAhd79CO8i6xe/STyW</span>
	    <span class="s0">4O7KVRgUJ/sP0heeJV4kEVKw/vZd40sFKxat4mLvp6VLdvnb/XHHGGPIKwBBpC1/9n</span>
	    <span class="s0">3DpfRZnn9/AwCxRII9O79kVPdjvByxuET6Ai8mePeTt4lyheXzhOSpCcdWa00uckTG</span>
	    <span class="s0">kckbGu76nEhbIm2xznH4VB3OWYaiXqQn8GKSWGIMHuXyPL76LBcupmhp69pz4uMnXi</span>
	    <span class="s0">w4VloTGcdQRtGdzmHs1f+RdYZslMZJhzUOHVnceN1ooEiP5JUzdqCQMWCD0JCIeQzn</span>
	    <span class="s0">NNpO+clhrCYf5rC+A2cxWmDUWG2oHEOZMEKIwclgMnnLrTeXUV7sUzpNXgU9DmijWV</span>
	    <span class="s0">v9LEKCkAIhKIBnlvpks6F21qUZ31u/sbExPa9h0/RzwzMov2nGlG5TmW1YOzzlnSfL</span>
	    <span class="s0">mVnyGf19Q7lwZHBp+1fPtflAIgiC7389n9qkihP+lWyeqfUO15ZwQTqlw9H+o2cOvN</span>
	    <span class="s0">QJCAHEgEqgYnI0NyALjAJdyWQy7wMa6AEujUdzo3LjcAXwD/XCTKIRjWytAAAAJXRF</span>
	    <span class="s0">WHRjcmVhdGUtZGF0ZQAyMDA5LTA0LTA2VDIxOjI1OjQxLTAzOjAw8s+uCAAAACV0RV</span>
	    <span class="s0">h0bW9kaWZ5LWRhdGUAMjAwOC0wMS0wM1QxNTowODoyMS0wMjowMJEc/44AAAAZdEVY</span>
	    <span class="s0">dFNvZnR3YXJlAHd3dy5pbmtzY2FwZS5vcmeb7jwaAAAAAElFTkSuQmCC</span>
	<span class="s0">}]</span>
    <span class="s0">}</span>
    <span class="s0">if {![info exists ::tk::Priv(folderImage)]} {</span>
	<span class="s0">set ::tk::Priv(folderImage) [image create photo -data {</span>
	    <span class="s0">iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAABHNCSVQICAgIfAhkiA</span>
	    <span class="s0">AAAAlwSFlzAAAN1wAADdcBQiibeAAAABl0RVh0U29mdHdhcmUAd3d3Lmlua3NjYXBl</span>
	    <span class="s0">Lm9yZ5vuPBoAAAHCSURBVDiNpZAxa5NRFIafc+9XLCni4BC6FBycMnbrLpkcgtDVX6</span>
	    <span class="s0">C70D/g4lZX/4coxLlgxFkpiiSSUGm/JiXfveee45AmNlhawXc53HvPee55X+l2u/yP</span>
	    <span class="s0">qt3d3Tfu/viatwt3fzIYDI5uBJhZr9fr3TMzzAx3B+D09PR+v98/7HQ6z5fNOWdCCG</span>
	    <span class="s0">U4HH6s67oAVDlnV1UmkwmllBUkhMD29nYHeLuEAkyn06qU8qqu64MrgIyqYmZrkHa7</span>
	    <span class="s0">3drc3KTVahFjJITAaDRiPB4/XFlQVVMtHH5IzJo/P4EA4MyB+erWPQB7++zs7ccYvl</span>
	    <span class="s0">U5Z08pMW2cl88eIXLZeDUpXzsBkNQ5eP1+p0opmaoCTgzw6fjs6gLLsp58FB60t0Dc</span>
	    <span class="s0">K1Ul54yIEIMQ43Uj68pquDmCeJVztpwzuBNE2LgBoMVpslHMCUEAFgDVxQbzVAiA+a</span>
	    <span class="s0">K5uGPmmDtZF3VpoUm2ArhqQaRiUjcMf81p1G60UEVhcjZfAFTVUkrgkS+jc06mDX9n</span>
	    <span class="s0">vq4YhJ9nlxZExMwMEaHJRutOdWuIIsJFUoBSuTvHJ4YIfP46unV4qdlsjsBRZRtb/X</span>
	    <span class="s0">fHd5+C8+P7+J8BIoxFwovfRxYhnhxjpzEAAAAASUVORK5CYII=</span>
	<span class="s0">}]</span>
    <span class="s0">}</span>
    <span class="s0">if {![info exists ::tk::Priv(fileImage)]} {</span>
	<span class="s0">set ::tk::Priv(fileImage)   [image create photo -data {</span>
	    <span class="s0">iVBORw0KGgoAAAANSUhEUgAAABAAAAAQCAYAAAAf8/9hAAAABmJLR0QA/wD/AP+gva</span>
	    <span class="s0">eTAAAACXBIWXMAAAsTAAALEwEAmpwYAAAAB3RJTUUH1QQWFA84umAmQgAAANpJREFU</span>
	    <span class="s0">OMutkj1uhDAQhb8HSLtbISGfgZ+zbJkix0HmFhwhUdocBnMBGvqtTIqIFSReWKK8ai</span>
	    <span class="s0">x73nwzHrVt+zEMwwvH9FrX9TsA1trpqKy10+yUzME4jnjvAZB0LzXHkojjmDRNVyh3</span>
	    <span class="s0">A+89zrlVwlKSqKrqVy/J8lAUxSZBSMny4ZLgp54iyPM8UPHGNJ2IomibAKDv+9VlWZ</span>
	    <span class="s0">bABbgB5/0WQgSSkC4PF2JF4JzbHN430c4vhAm0TyCJruuClefph4yCBCGT3T3Isoy/</span>
	    <span class="s0">KDHGfDZNcz2SZIx547/0BVRRX7n8uT/sAAAAAElFTkSuQmCC</span>
	<span class="s0">}]</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file:: --</span>
<span class="s0">#</span>
<span class="s0">#	Implements the TK file selection dialog.  This dialog is used when the</span>
<span class="s0">#	tk_strictMotif flag is set to false.  This procedure shouldn't be</span>
<span class="s0">#	called directly.  Call tk_getOpenFile or tk_getSaveFile instead.</span>
<span class="s0">#</span>
<span class="s0"># Arguments:</span>
<span class="s0">#	type		&quot;open&quot; or &quot;save&quot;</span>
<span class="s0">#	args		Options parsed by the procedure.</span>
<span class="s0">#</span>

<span class="s0">proc ::tk::dialog::file:: {type args} {</span>
    <span class="s0">variable ::tk::Priv</span>
    <span class="s0">variable showHiddenBtn</span>
    <span class="s0">set dataName __tk_filedialog</span>
    <span class="s0">upvar ::tk::dialog::file::$dataName data</span>

    <span class="s0">Config $dataName $type $args</span>

    <span class="s0">if {$data(-parent) eq &quot;.&quot;} {</span>
	<span class="s0">set w .$dataName</span>
    <span class="s0">} else {</span>
	<span class="s0">set w $data(-parent).$dataName</span>
    <span class="s0">}</span>

    <span class="s0"># (re)create the dialog box if necessary</span>
    <span class="s0">#</span>
    <span class="s0">if {![winfo exists $w]} {</span>
	<span class="s0">Create $w TkFDialog</span>
    <span class="s0">} elseif {[winfo class $w] ne &quot;TkFDialog&quot;} {</span>
	<span class="s0">destroy $w</span>
	<span class="s0">Create $w TkFDialog</span>
    <span class="s0">} else {</span>
	<span class="s0">set data(dirMenuBtn) $w.contents.f1.menu</span>
	<span class="s0">set data(dirMenu) $w.contents.f1.menu.menu</span>
	<span class="s0">set data(upBtn) $w.contents.f1.up</span>
	<span class="s0">set data(icons) $w.contents.icons</span>
	<span class="s0">set data(ent) $w.contents.f2.ent</span>
	<span class="s0">set data(typeMenuLab) $w.contents.f2.lab2</span>
	<span class="s0">set data(typeMenuBtn) $w.contents.f2.menu</span>
	<span class="s0">set data(typeMenu) $data(typeMenuBtn).m</span>
	<span class="s0">set data(okBtn) $w.contents.f2.ok</span>
	<span class="s0">set data(cancelBtn) $w.contents.f2.cancel</span>
	<span class="s0">set data(hiddenBtn) $w.contents.f2.hidden</span>
	<span class="s0">SetSelectMode $w $data(-multiple)</span>
    <span class="s0">}</span>
    <span class="s0">if {$showHiddenBtn} {</span>
	<span class="s0">$data(hiddenBtn) configure -state normal</span>
	<span class="s0">grid $data(hiddenBtn)</span>
    <span class="s0">} else {</span>
	<span class="s0">$data(hiddenBtn) configure -state disabled</span>
	<span class="s0">grid remove $data(hiddenBtn)</span>
    <span class="s0">}</span>

    <span class="s0"># Make sure subseqent uses of this dialog are independent [Bug 845189]</span>
    <span class="s0">unset -nocomplain data(extUsed)</span>

    <span class="s0"># Dialog boxes should be transient with respect to their parent, so that</span>
    <span class="s0"># they will always stay on top of their parent window.  However, some</span>
    <span class="s0"># window managers will create the window as withdrawn if the parent window</span>
    <span class="s0"># is withdrawn or iconified.  Combined with the grab we put on the window,</span>
    <span class="s0"># this can hang the entire application.  Therefore we only make the dialog</span>
    <span class="s0"># transient if the parent is viewable.</span>

    <span class="s0">if {[winfo viewable [winfo toplevel $data(-parent)]]} {</span>
	<span class="s0">wm transient $w $data(-parent)</span>
    <span class="s0">}</span>

    <span class="s0"># Add traces on the selectPath variable</span>
    <span class="s0">#</span>

    <span class="s0">trace add variable data(selectPath) write \</span>
	    <span class="s0">[list ::tk::dialog::file::SetPath $w]</span>
    <span class="s0">$data(dirMenuBtn) configure \</span>
	    <span class="s0">-textvariable ::tk::dialog::file::${dataName}(selectPath)</span>

    <span class="s0"># Cleanup previous menu</span>
    <span class="s0">#</span>
    <span class="s0">$data(typeMenu) delete 0 end</span>
    <span class="s0">$data(typeMenuBtn) configure -state normal -text &quot;&quot;</span>

    <span class="s0"># Initialize the file types menu</span>
    <span class="s0">#</span>
    <span class="s0">if {[llength $data(-filetypes)]} {</span>
	<span class="s0"># Default type and name to first entry</span>
	<span class="s0">set initialtype     [lindex $data(-filetypes) 0]</span>
	<span class="s0">set initialTypeName [lindex $initialtype 0]</span>
	<span class="s0">if {$data(-typevariable) ne &quot;&quot;} {</span>
	    <span class="s0">upvar #0 $data(-typevariable) typeVariable</span>
	    <span class="s0">if {[info exists typeVariable]} {</span>
		<span class="s0">set initialTypeName $typeVariable</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
	<span class="s0">foreach type $data(-filetypes) {</span>
	    <span class="s0">set title  [lindex $type 0]</span>
	    <span class="s0">set filter [lindex $type 1]</span>
	    <span class="s0">$data(typeMenu) add command -label $title \</span>
		<span class="s0">-command [list ::tk::dialog::file::SetFilter $w $type]</span>
	    <span class="s0"># [string first] avoids glob-pattern char issues</span>
	    <span class="s0">if {[string first ${initialTypeName} $title] == 0} {</span>
		<span class="s0">set initialtype $type</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
	<span class="s0">SetFilter $w $initialtype</span>
	<span class="s0">$data(typeMenuBtn) configure -state normal</span>
	<span class="s0">$data(typeMenuLab) configure -state normal</span>
    <span class="s0">} else {</span>
	<span class="s0">set data(filter) &quot;*&quot;</span>
	<span class="s0">$data(typeMenuBtn) configure -state disabled -takefocus 0</span>
	<span class="s0">$data(typeMenuLab) configure -state disabled</span>
    <span class="s0">}</span>
    <span class="s0">UpdateWhenIdle $w</span>

    <span class="s0"># Withdraw the window, then update all the geometry information</span>
    <span class="s0"># so we know how big it wants to be, then center the window in the</span>
    <span class="s0"># display (Motif style) and de-iconify it.</span>

    <span class="s0">::tk::PlaceWindow $w widget $data(-parent)</span>
    <span class="s0">wm title $w $data(-title)</span>

    <span class="s0"># Set a grab and claim the focus too.</span>

    <span class="s0">::tk::SetFocusGrab $w $data(ent)</span>
    <span class="s0">$data(ent) delete 0 end</span>
    <span class="s0">$data(ent) insert 0 $data(selectFile)</span>
    <span class="s0">$data(ent) selection range 0 end</span>
    <span class="s0">$data(ent) icursor end</span>

    <span class="s0"># Wait for the user to respond, then restore the focus and return the</span>
    <span class="s0"># index of the selected button.  Restore the focus before deleting the</span>
    <span class="s0"># window, since otherwise the window manager may take the focus away so we</span>
    <span class="s0"># can't redirect it.  Finally, restore any grab that was in effect.</span>

    <span class="s0">vwait ::tk::Priv(selectFilePath)</span>

    <span class="s0">::tk::RestoreFocusGrab $w $data(ent) withdraw</span>

    <span class="s0"># Cleanup traces on selectPath variable</span>
    <span class="s0">#</span>

    <span class="s0">foreach trace [trace info variable data(selectPath)] {</span>
	<span class="s0">trace remove variable data(selectPath) {*}$trace</span>
    <span class="s0">}</span>
    <span class="s0">$data(dirMenuBtn) configure -textvariable {}</span>

    <span class="s0">return $Priv(selectFilePath)</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::Config --</span>
<span class="s0">#</span>
<span class="s0">#	Configures the TK filedialog according to the argument list</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::Config {dataName type argList} {</span>
    <span class="s0">upvar ::tk::dialog::file::$dataName data</span>

    <span class="s0">set data(type) $type</span>

    <span class="s0"># 0: Delete all variable that were set on data(selectPath) the</span>
    <span class="s0"># last time the file dialog is used. The traces may cause troubles</span>
    <span class="s0"># if the dialog is now used with a different -parent option.</span>

    <span class="s0">foreach trace [trace info variable data(selectPath)] {</span>
	<span class="s0">trace remove variable data(selectPath) {*}$trace</span>
    <span class="s0">}</span>

    <span class="s0"># 1: the configuration specs</span>
    <span class="s0">#</span>
    <span class="s0">set specs {</span>
	<span class="s0">{-defaultextension &quot;&quot; &quot;&quot; &quot;&quot;}</span>
	<span class="s0">{-filetypes &quot;&quot; &quot;&quot; &quot;&quot;}</span>
	<span class="s0">{-initialdir &quot;&quot; &quot;&quot; &quot;&quot;}</span>
	<span class="s0">{-initialfile &quot;&quot; &quot;&quot; &quot;&quot;}</span>
	<span class="s0">{-parent &quot;&quot; &quot;&quot; &quot;.&quot;}</span>
	<span class="s0">{-title &quot;&quot; &quot;&quot; &quot;&quot;}</span>
	<span class="s0">{-typevariable &quot;&quot; &quot;&quot; &quot;&quot;}</span>
    <span class="s0">}</span>

    <span class="s0"># The &quot;-multiple&quot; option is only available for the &quot;open&quot; file dialog.</span>
    <span class="s0">#</span>
    <span class="s0">if {$type eq &quot;open&quot;} {</span>
	<span class="s0">lappend specs {-multiple &quot;&quot; &quot;&quot; &quot;0&quot;}</span>
    <span class="s0">}</span>

    <span class="s0"># The &quot;-confirmoverwrite&quot; option is only for the &quot;save&quot; file dialog.</span>
    <span class="s0">#</span>
    <span class="s0">if {$type eq &quot;save&quot;} {</span>
	<span class="s0">lappend specs {-confirmoverwrite &quot;&quot; &quot;&quot; &quot;1&quot;}</span>
    <span class="s0">}</span>

    <span class="s0"># 2: default values depending on the type of the dialog</span>
    <span class="s0">#</span>
    <span class="s0">if {![info exists data(selectPath)]} {</span>
	<span class="s0"># first time the dialog has been popped up</span>
	<span class="s0">set data(selectPath) [pwd]</span>
	<span class="s0">set data(selectFile) &quot;&quot;</span>
    <span class="s0">}</span>

    <span class="s0"># 3: parse the arguments</span>
    <span class="s0">#</span>
    <span class="s0">tclParseConfigSpec ::tk::dialog::file::$dataName $specs &quot;&quot; $argList</span>

    <span class="s0">if {$data(-title) eq &quot;&quot;} {</span>
	<span class="s0">if {$type eq &quot;open&quot;} {</span>
	    <span class="s0">set data(-title) [mc &quot;Open&quot;]</span>
	<span class="s0">} else {</span>
	    <span class="s0">set data(-title) [mc &quot;Save As&quot;]</span>
	<span class="s0">}</span>
    <span class="s0">}</span>

    <span class="s0"># 4: set the default directory and selection according to the -initial</span>
    <span class="s0">#    settings</span>
    <span class="s0">#</span>
    <span class="s0">if {$data(-initialdir) ne &quot;&quot;} {</span>
	<span class="s0"># Ensure that initialdir is an absolute path name.</span>
	<span class="s0">if {[file isdirectory $data(-initialdir)]} {</span>
	    <span class="s0">set old [pwd]</span>
	    <span class="s0">cd $data(-initialdir)</span>
	    <span class="s0">set data(selectPath) [pwd]</span>
	    <span class="s0">cd $old</span>
	<span class="s0">} else {</span>
	    <span class="s0">set data(selectPath) [pwd]</span>
	<span class="s0">}</span>
    <span class="s0">}</span>
    <span class="s0">set data(selectFile) $data(-initialfile)</span>

    <span class="s0"># 5. Parse the -filetypes option</span>
    <span class="s0">#</span>
    <span class="s0">set data(-filetypes) [::tk::FDGetFileTypes $data(-filetypes)]</span>

    <span class="s0">if {![winfo exists $data(-parent)]} {</span>
	<span class="s0">return -code error -errorcode [list TK LOOKUP WINDOW $data(-parent)] \</span>
	    <span class="s0">&quot;bad window path name \&quot;$data(-parent)\&quot;&quot;</span>
    <span class="s0">}</span>

    <span class="s0"># Set -multiple to a one or zero value (not other boolean types like</span>
    <span class="s0"># &quot;yes&quot;) so we can use it in tests more easily.</span>
    <span class="s0">if {$type eq &quot;save&quot;} {</span>
	<span class="s0">set data(-multiple) 0</span>
    <span class="s0">} elseif {$data(-multiple)} {</span>
	<span class="s0">set data(-multiple) 1</span>
    <span class="s0">} else {</span>
	<span class="s0">set data(-multiple) 0</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0">proc ::tk::dialog::file::Create {w class} {</span>
    <span class="s0">set dataName [lindex [split $w .] end]</span>
    <span class="s0">upvar ::tk::dialog::file::$dataName data</span>
    <span class="s0">variable ::tk::Priv</span>
    <span class="s0">global tk_library</span>

    <span class="s0">toplevel $w -class $class</span>
    <span class="s0">if {[tk windowingsystem] eq &quot;x11&quot;} {wm attributes $w -type dialog}</span>
    <span class="s0">pack [ttk::frame $w.contents] -expand 1 -fill both</span>
    <span class="s0">#set w $w.contents</span>

    <span class="s0"># f1: the frame with the directory option menu</span>
    <span class="s0">#</span>
    <span class="s0">set f1 [ttk::frame $w.contents.f1]</span>
    <span class="s0">bind [::tk::AmpWidget ttk::label $f1.lab -text [mc &quot;&amp;Directory:&quot;]] \</span>
	    <span class="s0">&lt;&lt;AltUnderlined&gt;&gt; [list focus $f1.menu]</span>

    <span class="s0">set data(dirMenuBtn) $f1.menu</span>
    <span class="s0">if {![info exists data(selectPath)]} {</span>
	<span class="s0">set data(selectPath) &quot;&quot;</span>
    <span class="s0">}</span>
    <span class="s0">set data(dirMenu) $f1.menu.menu</span>
    <span class="s0">ttk::menubutton $f1.menu -menu $data(dirMenu) -direction flush \</span>
	    <span class="s0">-textvariable [format %s(selectPath) ::tk::dialog::file::$dataName]</span>
    <span class="s0">menu $data(dirMenu) -tearoff 0</span>
    <span class="s0">$data(dirMenu) add radiobutton -label &quot;&quot; -variable \</span>
	    <span class="s0">[format %s(selectPath) ::tk::dialog::file::$dataName]</span>
    <span class="s0">set data(upBtn) [ttk::button $f1.up]</span>
    <span class="s0">$data(upBtn) configure -image $Priv(updirImage)</span>

    <span class="s0">$f1.menu configure -takefocus 1;# -highlightthickness 2</span>

    <span class="s0">pack $data(upBtn) -side right -padx 4 -fill both</span>
    <span class="s0">pack $f1.lab -side left -padx 4 -fill both</span>
    <span class="s0">pack $f1.menu -expand yes -fill both -padx 4</span>

    <span class="s0"># data(icons): the IconList that list the files and directories.</span>
    <span class="s0">#</span>
    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">if { $data(-multiple) } {</span>
	    <span class="s0">set fNameCaption [mc &quot;File &amp;names:&quot;]</span>
	<span class="s0">} else {</span>
	    <span class="s0">set fNameCaption [mc &quot;File &amp;name:&quot;]</span>
	<span class="s0">}</span>
	<span class="s0">set fTypeCaption [mc &quot;Files of &amp;type:&quot;]</span>
	<span class="s0">set iconListCommand [list ::tk::dialog::file::OkCmd $w]</span>
    <span class="s0">} else {</span>
	<span class="s0">set fNameCaption [mc &quot;&amp;Selection:&quot;]</span>
	<span class="s0">set iconListCommand [list ::tk::dialog::file::chooseDir::DblClick $w]</span>
    <span class="s0">}</span>
    <span class="s0">set data(icons) [::tk::IconList $w.contents.icons \</span>
	    <span class="s0">-command $iconListCommand -multiple $data(-multiple)]</span>
    <span class="s0">bind $data(icons) &lt;&lt;ListboxSelect&gt;&gt; \</span>
	    <span class="s0">[list ::tk::dialog::file::ListBrowse $w]</span>

    <span class="s0"># f2: the frame with the OK button, cancel button, &quot;file name&quot; field</span>
    <span class="s0">#     and file types field.</span>
    <span class="s0">#</span>
    <span class="s0">set f2 [ttk::frame $w.contents.f2]</span>
    <span class="s0">bind [::tk::AmpWidget ttk::label $f2.lab -text $fNameCaption -anchor e]\</span>
	    <span class="s0">&lt;&lt;AltUnderlined&gt;&gt; [list focus $f2.ent]</span>
    <span class="s0"># -pady 0</span>
    <span class="s0">set data(ent) [ttk::entry $f2.ent]</span>

    <span class="s0"># The font to use for the icons. The default Canvas font on Unix is just</span>
    <span class="s0"># deviant.</span>
    <span class="s0">set ::tk::$w.contents.icons(font) [$data(ent) cget -font]</span>

    <span class="s0"># Make the file types bits only if this is a File Dialog</span>
    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">set data(typeMenuLab) [::tk::AmpWidget ttk::label $f2.lab2 \</span>
		<span class="s0">-text $fTypeCaption -anchor e]</span>
	<span class="s0"># -pady [$f2.lab cget -pady]</span>
	<span class="s0">set data(typeMenuBtn) [ttk::menubutton $f2.menu \</span>
		<span class="s0">-menu $f2.menu.m]</span>
	<span class="s0"># -indicatoron 1</span>
	<span class="s0">set data(typeMenu) [menu $data(typeMenuBtn).m -tearoff 0]</span>
	<span class="s0"># $data(typeMenuBtn) configure -takefocus 1 -relief raised -anchor w</span>
	<span class="s0">bind $data(typeMenuLab) &lt;&lt;AltUnderlined&gt;&gt; [list \</span>
		<span class="s0">focus $data(typeMenuBtn)]</span>
    <span class="s0">}</span>

    <span class="s0"># The hidden button is displayed when ::tk::dialog::file::showHiddenBtn is</span>
    <span class="s0"># true.  Create it disabled so the binding doesn't trigger if it isn't</span>
    <span class="s0"># shown.</span>
    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">set text [mc &quot;Show &amp;Hidden Files and Directories&quot;]</span>
    <span class="s0">} else {</span>
	<span class="s0">set text [mc &quot;Show &amp;Hidden Directories&quot;]</span>
    <span class="s0">}</span>
    <span class="s0">set data(hiddenBtn) [::tk::AmpWidget ttk::checkbutton $f2.hidden \</span>
	    <span class="s0">-text $text -state disabled \</span>
	    <span class="s0">-variable ::tk::dialog::file::showHiddenVar \</span>
	    <span class="s0">-command [list ::tk::dialog::file::UpdateWhenIdle $w]]</span>
<span class="s0"># -anchor w -padx 3</span>

    <span class="s0"># the okBtn is created after the typeMenu so that the keyboard traversal</span>
    <span class="s0"># is in the right order, and add binding so that we find out when the</span>
    <span class="s0"># dialog is destroyed by the user (added here instead of to the overall</span>
    <span class="s0"># window so no confusion about how much &lt;Destroy&gt; gets called; exactly</span>
    <span class="s0"># once will do). [Bug 987169]</span>

    <span class="s0">set data(okBtn)     [::tk::AmpWidget ttk::button $f2.ok \</span>
	    <span class="s0">-text [mc &quot;&amp;OK&quot;]     -default active];# -pady 3]</span>
    <span class="s0">bind $data(okBtn) &lt;Destroy&gt; [list ::tk::dialog::file::Destroyed $w]</span>
    <span class="s0">set data(cancelBtn) [::tk::AmpWidget ttk::button $f2.cancel \</span>
	    <span class="s0">-text [mc &quot;&amp;Cancel&quot;] -default normal];# -pady 3]</span>

    <span class="s0"># grid the widgets in f2</span>
    <span class="s0">#</span>
    <span class="s0">grid $f2.lab $f2.ent $data(okBtn) -padx 4 -pady 3 -sticky ew</span>
    <span class="s0">grid configure $f2.ent -padx 2</span>
    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">grid $data(typeMenuLab) $data(typeMenuBtn) $data(cancelBtn) \</span>
		<span class="s0">-padx 4 -sticky ew</span>
	<span class="s0">grid configure $data(typeMenuBtn) -padx 0</span>
	<span class="s0">grid $data(hiddenBtn) -columnspan 2 -padx 4 -sticky ew</span>
    <span class="s0">} else {</span>
	<span class="s0">grid $data(hiddenBtn) - $data(cancelBtn) -padx 4 -sticky ew</span>
    <span class="s0">}</span>
    <span class="s0">grid columnconfigure $f2 1 -weight 1</span>

    <span class="s0"># Pack all the frames together. We are done with widget construction.</span>
    <span class="s0">#</span>
    <span class="s0">pack $f1 -side top -fill x -pady 4</span>
    <span class="s0">pack $f2 -side bottom -pady 4 -fill x</span>
    <span class="s0">pack $data(icons) -expand yes -fill both -padx 4 -pady 1</span>

    <span class="s0"># Set up the event handlers that are common to Directory and File Dialogs</span>
    <span class="s0">#</span>

    <span class="s0">wm protocol $w WM_DELETE_WINDOW [list ::tk::dialog::file::CancelCmd $w]</span>
    <span class="s0">$data(upBtn)     configure -command [list ::tk::dialog::file::UpDirCmd $w]</span>
    <span class="s0">$data(cancelBtn) configure -command [list ::tk::dialog::file::CancelCmd $w]</span>
    <span class="s0">bind $w &lt;KeyPress-Escape&gt; [list $data(cancelBtn) invoke]</span>
    <span class="s0">bind $w &lt;Alt-Key&gt; [list tk::AltKeyInDialog $w %A]</span>

    <span class="s0"># Set up event handlers specific to File or Directory Dialogs</span>
    <span class="s0">#</span>
    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">bind $data(ent) &lt;Return&gt; [list ::tk::dialog::file::ActivateEnt $w]</span>
	<span class="s0">$data(okBtn)     configure -command [list ::tk::dialog::file::OkCmd $w]</span>
	<span class="s0">bind $w &lt;Alt-t&gt; [format {</span>
	    <span class="s0">if {[%s cget -state] eq &quot;normal&quot;} {</span>
		<span class="s0">focus %s</span>
	    <span class="s0">}</span>
	<span class="s0">} $data(typeMenuBtn) $data(typeMenuBtn)]</span>
    <span class="s0">} else {</span>
	<span class="s0">set okCmd [list ::tk::dialog::file::chooseDir::OkCmd $w]</span>
	<span class="s0">bind $data(ent) &lt;Return&gt; $okCmd</span>
	<span class="s0">$data(okBtn) configure -command $okCmd</span>
	<span class="s0">bind $w &lt;Alt-s&gt; [list focus $data(ent)]</span>
	<span class="s0">bind $w &lt;Alt-o&gt; [list $data(okBtn) invoke]</span>
    <span class="s0">}</span>
    <span class="s0">bind $w &lt;Alt-h&gt; [list $data(hiddenBtn) invoke]</span>
    <span class="s0">bind $data(ent) &lt;Tab&gt; [list ::tk::dialog::file::CompleteEnt $w]</span>

    <span class="s0"># Build the focus group for all the entries</span>
    <span class="s0">#</span>
    <span class="s0">::tk::FocusGroup_Create $w</span>
    <span class="s0">::tk::FocusGroup_BindIn $w  $data(ent) [list \</span>
	    <span class="s0">::tk::dialog::file::EntFocusIn $w]</span>
    <span class="s0">::tk::FocusGroup_BindOut $w $data(ent) [list \</span>
	    <span class="s0">::tk::dialog::file::EntFocusOut $w]</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::SetSelectMode --</span>
<span class="s0">#</span>
<span class="s0">#	Set the select mode of the dialog to single select or multi-select.</span>
<span class="s0">#</span>
<span class="s0"># Arguments:</span>
<span class="s0">#	w		The dialog path.</span>
<span class="s0">#	multi		1 if the dialog is multi-select; 0 otherwise.</span>
<span class="s0">#</span>
<span class="s0"># Results:</span>
<span class="s0">#	None.</span>

<span class="s0">proc ::tk::dialog::file::SetSelectMode {w multi} {</span>
    <span class="s0">set dataName __tk_filedialog</span>
    <span class="s0">upvar ::tk::dialog::file::$dataName data</span>
    <span class="s0">if { $multi } {</span>
	<span class="s0">set fNameCaption [mc &quot;File &amp;names:&quot;]</span>
    <span class="s0">} else {</span>
	<span class="s0">set fNameCaption [mc &quot;File &amp;name:&quot;]</span>
    <span class="s0">}</span>
    <span class="s0">set iconListCommand [list ::tk::dialog::file::OkCmd $w]</span>
    <span class="s0">::tk::SetAmpText $w.contents.f2.lab $fNameCaption</span>
    <span class="s0">$data(icons) configure -multiple $multi -command $iconListCommand</span>
    <span class="s0">return</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::UpdateWhenIdle --</span>
<span class="s0">#</span>
<span class="s0">#	Creates an idle event handler which updates the dialog in idle time.</span>
<span class="s0">#	This is important because loading the directory may take a long time</span>
<span class="s0">#	and we don't want to load the same directory for multiple times due to</span>
<span class="s0">#	multiple concurrent events.</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::UpdateWhenIdle {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">if {[info exists data(updateId)]} {</span>
	<span class="s0">return</span>
    <span class="s0">}</span>
    <span class="s0">set data(updateId) [after idle [list ::tk::dialog::file::Update $w]]</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::Update --</span>
<span class="s0">#</span>
<span class="s0">#	Loads the files and directories into the IconList widget. Also sets up</span>
<span class="s0">#	the directory option menu for quick access to parent directories.</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::Update {w} {</span>
    <span class="s0"># This proc may be called within an idle handler. Make sure that the</span>
    <span class="s0"># window has not been destroyed before this proc is called</span>
    <span class="s0">if {![winfo exists $w]} {</span>
	<span class="s0">return</span>
    <span class="s0">}</span>
    <span class="s0">set class [winfo class $w]</span>
    <span class="s0">if {($class ne &quot;TkFDialog&quot;) &amp;&amp; ($class ne &quot;TkChooseDir&quot;)} {</span>
	<span class="s0">return</span>
    <span class="s0">}</span>

    <span class="s0">set dataName [winfo name $w]</span>
    <span class="s0">upvar ::tk::dialog::file::$dataName data</span>
    <span class="s0">variable ::tk::Priv</span>
    <span class="s0">variable showHiddenVar</span>
    <span class="s0">global tk_library</span>
    <span class="s0">unset -nocomplain data(updateId)</span>

    <span class="s0">set folder $Priv(folderImage)</span>
    <span class="s0">set file   $Priv(fileImage)</span>

    <span class="s0">set appPWD [pwd]</span>
    <span class="s0">if {[catch {</span>
	<span class="s0">cd $data(selectPath)</span>
    <span class="s0">}]} then {</span>
	<span class="s0"># We cannot change directory to $data(selectPath). $data(selectPath)</span>
	<span class="s0"># should have been checked before ::tk::dialog::file::Update is</span>
	<span class="s0"># called, so we normally won't come to here. Anyways, give an error</span>
	<span class="s0"># and abort action.</span>
	<span class="s0">tk_messageBox -type ok -parent $w -icon warning -message [mc \</span>
	       <span class="s0">&quot;Cannot change to the directory \&quot;%1\$s\&quot;.\nPermission denied.&quot;\</span>
		<span class="s0">$data(selectPath)]</span>
	<span class="s0">cd $appPWD</span>
	<span class="s0">return</span>
    <span class="s0">}</span>

    <span class="s0"># Turn on the busy cursor. BUG?? We haven't disabled X events, though,</span>
    <span class="s0"># so the user may still click and cause havoc ...</span>
    <span class="s0">#</span>
    <span class="s0">set entCursor [$data(ent) cget -cursor]</span>
    <span class="s0">set dlgCursor [$w         cget -cursor]</span>
    <span class="s0">$data(ent) configure -cursor watch</span>
    <span class="s0">$w         configure -cursor watch</span>
    <span class="s0">update idletasks</span>

    <span class="s0">$data(icons) deleteall</span>

    <span class="s0">set showHidden $showHiddenVar</span>

    <span class="s0"># Make the dir list. Note that using an explicit [pwd] (instead of '.') is</span>
    <span class="s0"># better in some VFS cases.</span>
    <span class="s0">$data(icons) add $folder [GlobFiltered [pwd] d 1]</span>

    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0"># Make the file list if this is a File Dialog, selecting all but</span>
	<span class="s0"># 'd'irectory type files.</span>
	<span class="s0">#</span>
	<span class="s0">$data(icons) add $file [GlobFiltered [pwd] {f b c l p s}]</span>
    <span class="s0">}</span>

    <span class="s0"># Update the Directory: option menu</span>
    <span class="s0">#</span>
    <span class="s0">set list &quot;&quot;</span>
    <span class="s0">set dir &quot;&quot;</span>
    <span class="s0">foreach subdir [file split $data(selectPath)] {</span>
	<span class="s0">set dir [file join $dir $subdir]</span>
	<span class="s0">lappend list $dir</span>
    <span class="s0">}</span>

    <span class="s0">$data(dirMenu) delete 0 end</span>
    <span class="s0">set var [format %s(selectPath) ::tk::dialog::file::$dataName]</span>
    <span class="s0">foreach path $list {</span>
	<span class="s0">$data(dirMenu) add command -label $path -command [list set $var $path]</span>
    <span class="s0">}</span>

    <span class="s0"># Restore the PWD to the application's PWD</span>
    <span class="s0">#</span>
    <span class="s0">cd $appPWD</span>

    <span class="s0">if {$class eq &quot;TkFDialog&quot;} {</span>
	<span class="s0"># Restore the Open/Save Button if this is a File Dialog</span>
	<span class="s0">#</span>
	<span class="s0">if {$data(type) eq &quot;open&quot;} {</span>
	    <span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Open&quot;]</span>
	<span class="s0">} else {</span>
	    <span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Save&quot;]</span>
	<span class="s0">}</span>
    <span class="s0">}</span>

    <span class="s0"># turn off the busy cursor.</span>
    <span class="s0">#</span>
    <span class="s0">$data(ent) configure -cursor $entCursor</span>
    <span class="s0">$w         configure -cursor $dlgCursor</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::SetPathSilently --</span>
<span class="s0">#</span>
<span class="s0"># 	Sets data(selectPath) without invoking the trace procedure</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::SetPathSilently {w path} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">set cb [list ::tk::dialog::file::SetPath $w]</span>
    <span class="s0">trace remove variable data(selectPath) write $cb</span>
    <span class="s0">set data(selectPath) $path</span>
    <span class="s0">trace add variable data(selectPath) write $cb</span>
<span class="s0">}</span>


<span class="s0"># This proc gets called whenever data(selectPath) is set</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::SetPath {w name1 name2 op} {</span>
    <span class="s0">if {[winfo exists $w]} {</span>
	<span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>
	<span class="s0">UpdateWhenIdle $w</span>
	<span class="s0"># On directory dialogs, we keep the entry in sync with the currentdir.</span>
	<span class="s0">if {[winfo class $w] eq &quot;TkChooseDir&quot;} {</span>
	    <span class="s0">$data(ent) delete 0 end</span>
	    <span class="s0">$data(ent) insert end $data(selectPath)</span>
	<span class="s0">}</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># This proc gets called whenever data(filter) is set</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::SetFilter {w type} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">set data(filterType) $type</span>
    <span class="s0">set data(filter) [lindex $type 1]</span>
    <span class="s0">$data(typeMenuBtn) configure -text [lindex $type 0] ;#-indicatoron 1</span>

    <span class="s0"># If we aren't using a default extension, use the one suppled by the</span>
    <span class="s0"># filter.</span>
    <span class="s0">if {![info exists data(extUsed)]} {</span>
	<span class="s0">if {[string length $data(-defaultextension)]} {</span>
	    <span class="s0">set data(extUsed) 1</span>
	<span class="s0">} else {</span>
	    <span class="s0">set data(extUsed) 0</span>
	<span class="s0">}</span>
    <span class="s0">}</span>

    <span class="s0">if {!$data(extUsed)} {</span>
	<span class="s0"># Get the first extension in the list that matches {^\*\.\w+$} and</span>
	<span class="s0"># remove all * from the filter.</span>
	<span class="s0">set index [lsearch -regexp $data(filter) {^\*\.\w+$}]</span>
	<span class="s0">if {$index &gt;= 0} {</span>
	    <span class="s0">set data(-defaultextension) \</span>
		    <span class="s0">[string trimleft [lindex $data(filter) $index] &quot;*&quot;]</span>
	<span class="s0">} else {</span>
	    <span class="s0"># Couldn't find anything!  Reset to a safe default...</span>
	    <span class="s0">set data(-defaultextension) &quot;&quot;</span>
	<span class="s0">}</span>
    <span class="s0">}</span>

    <span class="s0">$data(icons) see 0</span>

    <span class="s0">UpdateWhenIdle $w</span>
<span class="s0">}</span>

<span class="s0"># tk::dialog::file::ResolveFile --</span>
<span class="s0">#</span>
<span class="s0">#	Interpret the user's text input in a file selection dialog.  Performs:</span>
<span class="s0">#</span>
<span class="s0">#	(1) ~ substitution</span>
<span class="s0">#	(2) resolve all instances of . and ..</span>
<span class="s0">#	(3) check for non-existent files/directories</span>
<span class="s0">#	(4) check for chdir permissions</span>
<span class="s0">#	(5) conversion of environment variable references to their</span>
<span class="s0">#	    contents (once only)</span>
<span class="s0">#</span>
<span class="s0"># Arguments:</span>
<span class="s0">#	context:  the current directory you are in</span>
<span class="s0">#	text:	  the text entered by the user</span>
<span class="s0">#	defaultext: the default extension to add to files with no extension</span>
<span class="s0">#	expandEnv: whether to expand environment variables (yes by default)</span>
<span class="s0">#</span>
<span class="s0"># Return vaue:</span>
<span class="s0">#	[list $flag $directory $file]</span>
<span class="s0">#</span>
<span class="s0">#	 flag = OK	: valid input</span>
<span class="s0">#	      = PATTERN	: valid directory/pattern</span>
<span class="s0">#	      = PATH	: the directory does not exist</span>
<span class="s0">#	      = FILE	: the directory exists by the file doesn't exist</span>
<span class="s0">#	      = CHDIR	: Cannot change to the directory</span>
<span class="s0">#	      = ERROR	: Invalid entry</span>
<span class="s0">#</span>
<span class="s0">#	 directory      : valid only if flag = OK or PATTERN or FILE</span>
<span class="s0">#	 file           : valid only if flag = OK or PATTERN</span>
<span class="s0">#</span>
<span class="s0">#	directory may not be the same as context, because text may contain a</span>
<span class="s0">#	subdirectory name</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::ResolveFile {context text defaultext {expandEnv 1}} {</span>
    <span class="s0">set appPWD [pwd]</span>

    <span class="s0">set path [JoinFile $context $text]</span>

    <span class="s0"># If the file has no extension, append the default.  Be careful not to do</span>
    <span class="s0"># this for directories, otherwise typing a dirname in the box will give</span>
    <span class="s0"># back &quot;dirname.extension&quot; instead of trying to change dir.</span>
    <span class="s0">if {</span>
	<span class="s0">![file isdirectory $path] &amp;&amp; ([file ext $path] eq &quot;&quot;) &amp;&amp;</span>
	<span class="s0">![string match {$*} [file tail $path]]</span>
    <span class="s0">} then {</span>
	<span class="s0">set path &quot;$path$defaultext&quot;</span>
    <span class="s0">}</span>

    <span class="s0">if {[catch {file exists $path}]} {</span>
	<span class="s0"># This &quot;if&quot; block can be safely removed if the following code stop</span>
	<span class="s0"># generating errors.</span>
	<span class="s0">#</span>
	<span class="s0">#	file exists ~nonsuchuser</span>
	<span class="s0">#</span>
	<span class="s0">return [list ERROR $path &quot;&quot;]</span>
    <span class="s0">}</span>

    <span class="s0">if {[file exists $path]} {</span>
	<span class="s0">if {[file isdirectory $path]} {</span>
	    <span class="s0">if {[catch {cd $path}]} {</span>
		<span class="s0">return [list CHDIR $path &quot;&quot;]</span>
	    <span class="s0">}</span>
	    <span class="s0">set directory [pwd]</span>
	    <span class="s0">set file &quot;&quot;</span>
	    <span class="s0">set flag OK</span>
	    <span class="s0">cd $appPWD</span>
	<span class="s0">} else {</span>
	    <span class="s0">if {[catch {cd [file dirname $path]}]} {</span>
		<span class="s0">return [list CHDIR [file dirname $path] &quot;&quot;]</span>
	    <span class="s0">}</span>
	    <span class="s0">set directory [pwd]</span>
	    <span class="s0">set file [file tail $path]</span>
	    <span class="s0">set flag OK</span>
	    <span class="s0">cd $appPWD</span>
	<span class="s0">}</span>
    <span class="s0">} else {</span>
	<span class="s0">set dirname [file dirname $path]</span>
	<span class="s0">if {[file exists $dirname]} {</span>
	    <span class="s0">if {[catch {cd $dirname}]} {</span>
		<span class="s0">return [list CHDIR $dirname &quot;&quot;]</span>
	    <span class="s0">}</span>
	    <span class="s0">set directory [pwd]</span>
	    <span class="s0">cd $appPWD</span>
	    <span class="s0">set file [file tail $path]</span>
	    <span class="s0"># It's nothing else, so check to see if it is an env-reference</span>
	    <span class="s0">if {$expandEnv &amp;&amp; [string match {$*} $file]} {</span>
		<span class="s0">set var [string range $file 1 end]</span>
		<span class="s0">if {[info exist ::env($var)]} {</span>
		    <span class="s0">return [ResolveFile $context $::env($var) $defaultext 0]</span>
		<span class="s0">}</span>
	    <span class="s0">}</span>
	    <span class="s0">if {[regexp {[*?]} $file]} {</span>
		<span class="s0">set flag PATTERN</span>
	    <span class="s0">} else {</span>
		<span class="s0">set flag FILE</span>
	    <span class="s0">}</span>
	<span class="s0">} else {</span>
	    <span class="s0">set directory $dirname</span>
	    <span class="s0">set file [file tail $path]</span>
	    <span class="s0">set flag PATH</span>
	    <span class="s0"># It's nothing else, so check to see if it is an env-reference</span>
	    <span class="s0">if {$expandEnv &amp;&amp; [string match {$*} $file]} {</span>
		<span class="s0">set var [string range $file 1 end]</span>
		<span class="s0">if {[info exist ::env($var)]} {</span>
		    <span class="s0">return [ResolveFile $context $::env($var) $defaultext 0]</span>
		<span class="s0">}</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
    <span class="s0">}</span>

    <span class="s0">return [list $flag $directory $file]</span>
<span class="s0">}</span>


<span class="s0"># Gets called when the entry box gets keyboard focus. We clear the selection</span>
<span class="s0"># from the icon list . This way the user can be certain that the input in the</span>
<span class="s0"># entry box is the selection.</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::EntFocusIn {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">if {[$data(ent) get] ne &quot;&quot;} {</span>
	<span class="s0">$data(ent) selection range 0 end</span>
	<span class="s0">$data(ent) icursor end</span>
    <span class="s0">} else {</span>
	<span class="s0">$data(ent) selection clear</span>
    <span class="s0">}</span>

    <span class="s0">if {[winfo class $w] eq &quot;TkFDialog&quot;} {</span>
	<span class="s0"># If this is a File Dialog, make sure the buttons are labeled right.</span>
	<span class="s0">if {$data(type) eq &quot;open&quot;} {</span>
	    <span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Open&quot;]</span>
	<span class="s0">} else {</span>
	    <span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Save&quot;]</span>
	<span class="s0">}</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0">proc ::tk::dialog::file::EntFocusOut {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">$data(ent) selection clear</span>
<span class="s0">}</span>


<span class="s0"># Gets called when user presses Return in the &quot;File name&quot; entry.</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::ActivateEnt {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">set text [$data(ent) get]</span>
    <span class="s0">if {$data(-multiple)} {</span>
	<span class="s0">foreach t $text {</span>
	    <span class="s0">VerifyFileName $w $t</span>
	<span class="s0">}</span>
    <span class="s0">} else {</span>
	<span class="s0">VerifyFileName $w $text</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># Verification procedure</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::VerifyFileName {w filename} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">set list [ResolveFile $data(selectPath) $filename $data(-defaultextension)]</span>
    <span class="s0">foreach {flag path file} $list {</span>
	<span class="s0">break</span>
    <span class="s0">}</span>

    <span class="s0">switch -- $flag {</span>
	<span class="s0">OK {</span>
	    <span class="s0">if {$file eq &quot;&quot;} {</span>
		<span class="s0"># user has entered an existing (sub)directory</span>
		<span class="s0">set data(selectPath) $path</span>
		<span class="s0">$data(ent) delete 0 end</span>
	    <span class="s0">} else {</span>
		<span class="s0">SetPathSilently $w $path</span>
		<span class="s0">if {$data(-multiple)} {</span>
		    <span class="s0">lappend data(selectFile) $file</span>
		<span class="s0">} else {</span>
		    <span class="s0">set data(selectFile) $file</span>
		<span class="s0">}</span>
		<span class="s0">Done $w</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
	<span class="s0">PATTERN {</span>
	    <span class="s0">set data(selectPath) $path</span>
	    <span class="s0">set data(filter) $file</span>
	<span class="s0">}</span>
	<span class="s0">FILE {</span>
	    <span class="s0">if {$data(type) eq &quot;open&quot;} {</span>
		<span class="s0">tk_messageBox -icon warning -type ok -parent $w \</span>
			<span class="s0">-message [mc &quot;File \&quot;%1\$s\&quot;  does not exist.&quot; \</span>
			<span class="s0">[file join $path $file]]</span>
		<span class="s0">$data(ent) selection range 0 end</span>
		<span class="s0">$data(ent) icursor end</span>
	    <span class="s0">} else {</span>
		<span class="s0">SetPathSilently $w $path</span>
		<span class="s0">if {$data(-multiple)} {</span>
		    <span class="s0">lappend data(selectFile) $file</span>
		<span class="s0">} else {</span>
		    <span class="s0">set data(selectFile) $file</span>
		<span class="s0">}</span>
		<span class="s0">Done $w</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
	<span class="s0">PATH {</span>
	    <span class="s0">tk_messageBox -icon warning -type ok -parent $w \</span>
		    <span class="s0">-message [mc &quot;Directory \&quot;%1\$s\&quot; does not exist.&quot; $path]</span>
	    <span class="s0">$data(ent) selection range 0 end</span>
	    <span class="s0">$data(ent) icursor end</span>
	<span class="s0">}</span>
	<span class="s0">CHDIR {</span>
	    <span class="s0">tk_messageBox -type ok -parent $w -icon warning -message  \</span>
		<span class="s0">[mc &quot;Cannot change to the directory\</span>
                     <span class="s0">\&quot;%1\$s\&quot;.\nPermission denied.&quot; $path]</span>
	    <span class="s0">$data(ent) selection range 0 end</span>
	    <span class="s0">$data(ent) icursor end</span>
	<span class="s0">}</span>
	<span class="s0">ERROR {</span>
	    <span class="s0">tk_messageBox -type ok -parent $w -icon warning -message \</span>
		    <span class="s0">[mc &quot;Invalid file name \&quot;%1\$s\&quot;.&quot; $path]</span>
	    <span class="s0">$data(ent) selection range 0 end</span>
	    <span class="s0">$data(ent) icursor end</span>
	<span class="s0">}</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user presses the Alt-s or Alt-o keys.</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::InvokeBtn {w key} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">if {[$data(okBtn) cget -text] eq $key} {</span>
	<span class="s0">$data(okBtn) invoke</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user presses the &quot;parent directory&quot; button</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::UpDirCmd {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">if {$data(selectPath) ne &quot;/&quot;} {</span>
	<span class="s0">set data(selectPath) [file dirname $data(selectPath)]</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># Join a file name to a path name. The &quot;file join&quot; command will break if the</span>
<span class="s0"># filename begins with ~</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::JoinFile {path file} {</span>
    <span class="s0">if {[string match {~*} $file] &amp;&amp; [file exists $path/$file]} {</span>
	<span class="s0">return [file join $path ./$file]</span>
    <span class="s0">} else {</span>
	<span class="s0">return [file join $path $file]</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user presses the &quot;OK&quot; button</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::OkCmd {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">set filenames {}</span>
    <span class="s0">foreach item [$data(icons) selection get] {</span>
	<span class="s0">lappend filenames [$data(icons) get $item]</span>
    <span class="s0">}</span>

    <span class="s0">if {</span>
	<span class="s0">([llength $filenames] &amp;&amp; !$data(-multiple)) ||</span>
	<span class="s0">($data(-multiple) &amp;&amp; ([llength $filenames] == 1))</span>
    <span class="s0">} then {</span>
	<span class="s0">set filename [lindex $filenames 0]</span>
	<span class="s0">set file [JoinFile $data(selectPath) $filename]</span>
	<span class="s0">if {[file isdirectory $file]} {</span>
	    <span class="s0">ListInvoke $w [list $filename]</span>
	    <span class="s0">return</span>
	<span class="s0">}</span>
    <span class="s0">}</span>

    <span class="s0">ActivateEnt $w</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user presses the &quot;Cancel&quot; button</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::CancelCmd {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>
    <span class="s0">variable ::tk::Priv</span>

    <span class="s0">bind $data(okBtn) &lt;Destroy&gt; {}</span>
    <span class="s0">set Priv(selectFilePath) &quot;&quot;</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user destroys the dialog directly [Bug 987169]</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::Destroyed {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>
    <span class="s0">variable ::tk::Priv</span>

    <span class="s0">set Priv(selectFilePath) &quot;&quot;</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user browses the IconList widget (dragging mouse, arrow</span>
<span class="s0"># keys, etc)</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::ListBrowse {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">set text {}</span>
    <span class="s0">foreach item [$data(icons) selection get] {</span>
	<span class="s0">lappend text [$data(icons) get $item]</span>
    <span class="s0">}</span>
    <span class="s0">if {[llength $text] == 0} {</span>
	<span class="s0">return</span>
    <span class="s0">}</span>
    <span class="s0">if {$data(-multiple)} {</span>
	<span class="s0">set newtext {}</span>
	<span class="s0">foreach file $text {</span>
	    <span class="s0">set fullfile [JoinFile $data(selectPath) $file]</span>
	    <span class="s0">if { ![file isdirectory $fullfile] } {</span>
		<span class="s0">lappend newtext $file</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
	<span class="s0">set text $newtext</span>
	<span class="s0">set isDir 0</span>
    <span class="s0">} else {</span>
	<span class="s0">set text [lindex $text 0]</span>
	<span class="s0">set file [JoinFile $data(selectPath) $text]</span>
	<span class="s0">set isDir [file isdirectory $file]</span>
    <span class="s0">}</span>
    <span class="s0">if {!$isDir} {</span>
	<span class="s0">$data(ent) delete 0 end</span>
	<span class="s0">$data(ent) insert 0 $text</span>

	<span class="s0">if {[winfo class $w] eq &quot;TkFDialog&quot;} {</span>
	    <span class="s0">if {$data(type) eq &quot;open&quot;} {</span>
		<span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Open&quot;]</span>
	    <span class="s0">} else {</span>
		<span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Save&quot;]</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
    <span class="s0">} elseif {[winfo class $w] eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">::tk::SetAmpText $data(okBtn) [mc &quot;&amp;Open&quot;]</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># Gets called when user invokes the IconList widget (double-click, Return key,</span>
<span class="s0"># etc)</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::ListInvoke {w filenames} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>

    <span class="s0">if {[llength $filenames] == 0} {</span>
	<span class="s0">return</span>
    <span class="s0">}</span>

    <span class="s0">set file [JoinFile $data(selectPath) [lindex $filenames 0]]</span>

    <span class="s0">set class [winfo class $w]</span>
    <span class="s0">if {$class eq &quot;TkChooseDir&quot; || [file isdirectory $file]} {</span>
	<span class="s0">set appPWD [pwd]</span>
	<span class="s0">if {[catch {cd $file}]} {</span>
	    <span class="s0">tk_messageBox -type ok -parent $w -icon warning -message \</span>
		    <span class="s0">[mc &quot;Cannot change to the directory \&quot;%1\$s\&quot;.\nPermission denied.&quot; $file]</span>
	<span class="s0">} else {</span>
	    <span class="s0">cd $appPWD</span>
	    <span class="s0">set data(selectPath) $file</span>
	<span class="s0">}</span>
    <span class="s0">} else {</span>
	<span class="s0">if {$data(-multiple)} {</span>
	    <span class="s0">set data(selectFile) $filenames</span>
	<span class="s0">} else {</span>
	    <span class="s0">set data(selectFile) $file</span>
	<span class="s0">}</span>
	<span class="s0">Done $w</span>
    <span class="s0">}</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::Done --</span>
<span class="s0">#</span>
<span class="s0">#	Gets called when user has input a valid filename.  Pops up a dialog</span>
<span class="s0">#	box to confirm selection when necessary.  Sets the</span>
<span class="s0">#	tk::Priv(selectFilePath) variable, which will break the &quot;vwait&quot; loop</span>
<span class="s0">#	in ::tk::dialog::file:: and return the selected filename to the script</span>
<span class="s0">#	that calls tk_getOpenFile or tk_getSaveFile</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::Done {w {selectFilePath &quot;&quot;}} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>
    <span class="s0">variable ::tk::Priv</span>

    <span class="s0">if {$selectFilePath eq &quot;&quot;} {</span>
	<span class="s0">if {$data(-multiple)} {</span>
	    <span class="s0">set selectFilePath {}</span>
	    <span class="s0">foreach f $data(selectFile) {</span>
		<span class="s0">lappend selectFilePath [JoinFile $data(selectPath) $f]</span>
	    <span class="s0">}</span>
	<span class="s0">} else {</span>
	    <span class="s0">set selectFilePath [JoinFile $data(selectPath) $data(selectFile)]</span>
	<span class="s0">}</span>

	<span class="s0">set Priv(selectFile) $data(selectFile)</span>
	<span class="s0">set Priv(selectPath) $data(selectPath)</span>

	<span class="s0">if {($data(type) eq &quot;save&quot;) &amp;&amp; $data(-confirmoverwrite) &amp;&amp; [file exists $selectFilePath]} {</span>
	    <span class="s0">set reply [tk_messageBox -icon warning -type yesno -parent $w \</span>
		    <span class="s0">-message [mc &quot;File \&quot;%1\$s\&quot; already exists.\nDo you want\</span>
		    <span class="s0">to overwrite it?&quot; $selectFilePath]]</span>
	    <span class="s0">if {$reply eq &quot;no&quot;} {</span>
		<span class="s0">return</span>
	    <span class="s0">}</span>
	<span class="s0">}</span>
	<span class="s0">if {</span>
	    <span class="s0">[info exists data(-typevariable)] &amp;&amp; $data(-typevariable) ne &quot;&quot;</span>
	    <span class="s0">&amp;&amp; [info exists data(-filetypes)] &amp;&amp; [llength $data(-filetypes)]</span>
	    <span class="s0">&amp;&amp; [info exists data(filterType)] &amp;&amp; $data(filterType) ne &quot;&quot;</span>
	<span class="s0">} then {</span>
	    <span class="s0">upvar #0 $data(-typevariable) typeVariable</span>
	    <span class="s0">set typeVariable [lindex $data(filterType) 0]</span>
	<span class="s0">}</span>
    <span class="s0">}</span>
    <span class="s0">bind $data(okBtn) &lt;Destroy&gt; {}</span>
    <span class="s0">set Priv(selectFilePath) $selectFilePath</span>
<span class="s0">}</span>

<span class="s0"># ::tk::dialog::file::GlobFiltered --</span>
<span class="s0">#</span>
<span class="s0">#	Gets called to do globbing, returning the results and filtering them</span>
<span class="s0">#	according to the current filter (and removing the entries for '.' and</span>
<span class="s0">#	'..' which are never shown). Deals with evil cases such as where the</span>
<span class="s0">#	user is supplying a filter which is an invalid list or where it has an</span>
<span class="s0">#	unbalanced brace. The resulting list will be dictionary sorted.</span>
<span class="s0">#</span>
<span class="s0">#	Arguments:</span>
<span class="s0">#	  dir		 Which directory to search</span>
<span class="s0">#	  type		 List of filetypes to look for ('d' or 'f b c l p s')</span>
<span class="s0">#	  overrideFilter Whether to ignore the filter for this search.</span>
<span class="s0">#</span>
<span class="s0">#	NB: Assumes that the caller has mapped the state variable to 'data'.</span>
<span class="s0">#</span>
<span class="s0">proc ::tk::dialog::file::GlobFiltered {dir type {overrideFilter 0}} {</span>
    <span class="s0">variable showHiddenVar</span>
    <span class="s0">upvar 1 data(filter) filter</span>

    <span class="s0">if {$filter eq &quot;*&quot; || $overrideFilter} {</span>
	<span class="s0">set patterns [list *]</span>
	<span class="s0">if {$showHiddenVar} {</span>
	    <span class="s0">lappend patterns .*</span>
	<span class="s0">}</span>
    <span class="s0">} elseif {[string is list $filter]} {</span>
	<span class="s0">set patterns $filter</span>
    <span class="s0">} else {</span>
	<span class="s0"># Invalid list; assume we can use non-whitespace sequences as words</span>
	<span class="s0">set patterns [regexp -inline -all {\S+} $filter]</span>
    <span class="s0">}</span>

    <span class="s0">set opts [list -tails -directory $dir -type $type -nocomplain]</span>

    <span class="s0">set result {}</span>
    <span class="s0">catch {</span>
	<span class="s0"># We have a catch because we might have a really bad pattern (e.g.,</span>
	<span class="s0"># with an unbalanced brace); even [glob -nocomplain] doesn't like it.</span>
	<span class="s0"># Using a catch ensures that it just means we match nothing instead of</span>
	<span class="s0"># throwing a nasty error at the user...</span>
	<span class="s0">foreach f [glob {*}$opts -- {*}$patterns] {</span>
	    <span class="s0">if {$f eq &quot;.&quot; || $f eq &quot;..&quot;} {</span>
		<span class="s0">continue</span>
	    <span class="s0">}</span>
	    <span class="s0"># See ticket [1641721], $f might be a link pointing to a dir</span>
	    <span class="s0">if {$type != &quot;d&quot; &amp;&amp; [file isdir [file join $dir $f]]} {</span>
		<span class="s0">continue</span>
	    <span class="s0">}</span>
	    <span class="s0">lappend result $f</span>
	<span class="s0">}</span>
    <span class="s0">}</span>
    <span class="s0">return [lsort -dictionary -unique $result]</span>
<span class="s0">}</span>

<span class="s0">proc ::tk::dialog::file::CompleteEnt {w} {</span>
    <span class="s0">upvar ::tk::dialog::file::[winfo name $w] data</span>
    <span class="s0">set f [$data(ent) get]</span>
    <span class="s0">if {$data(-multiple)} {</span>
	<span class="s0">if {![string is list $f] || [llength $f] != 1} {</span>
	    <span class="s0">return -code break</span>
	<span class="s0">}</span>
	<span class="s0">set f [lindex $f 0]</span>
    <span class="s0">}</span>

    <span class="s0"># Get list of matching filenames and dirnames</span>
    <span class="s0">set files [if {[winfo class $w] eq &quot;TkFDialog&quot;} {</span>
	<span class="s0">GlobFiltered $data(selectPath) {f b c l p s}</span>
    <span class="s0">}]</span>
    <span class="s0">set dirs2 {}</span>
    <span class="s0">foreach d [GlobFiltered $data(selectPath) d] {lappend dirs2 $d/}</span>

    <span class="s0">set targets [concat \</span>
	    <span class="s0">[lsearch -glob -all -inline $files $f*] \</span>
	    <span class="s0">[lsearch -glob -all -inline $dirs2 $f*]]</span>

    <span class="s0">if {[llength $targets] == 1} {</span>
	<span class="s0"># We have a winner!</span>
	<span class="s0">set f [lindex $targets 0]</span>
    <span class="s0">} elseif {$f in $targets || [llength $targets] == 0} {</span>
	<span class="s0">if {[string length $f] &gt; 0} {</span>
	    <span class="s0">bell</span>
	<span class="s0">}</span>
	<span class="s0">return</span>
    <span class="s0">} elseif {[llength $targets] &gt; 1} {</span>
	<span class="s0"># Multiple possibles</span>
	<span class="s0">if {[string length $f] == 0} {</span>
	    <span class="s0">return</span>
	<span class="s0">}</span>
	<span class="s0">set t0 [lindex $targets 0]</span>
	<span class="s0">for {set len [string length $t0]} {$len&gt;0} {} {</span>
	    <span class="s0">set allmatch 1</span>
	    <span class="s0">foreach s $targets {</span>
		<span class="s0">if {![string equal -length $len $s $t0]} {</span>
		    <span class="s0">set allmatch 0</span>
		    <span class="s0">break</span>
		<span class="s0">}</span>
	    <span class="s0">}</span>
	    <span class="s0">incr len -1</span>
	    <span class="s0">if {$allmatch} break</span>
	<span class="s0">}</span>
	<span class="s0">set f [string range $t0 0 $len]</span>
    <span class="s0">}</span>

    <span class="s0">if {$data(-multiple)} {</span>
	<span class="s0">set f [list $f]</span>
    <span class="s0">}</span>
    <span class="s0">$data(ent) delete 0 end</span>
    <span class="s0">$data(ent) insert 0 $f</span>
    <span class="s0">return -code break</span>
<span class="s0">}</span>
</pre>
</body>
</html>